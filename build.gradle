buildscript {
	apply from: file('gradle/buildscript.gradle'), to: buildscript
}

apply plugin: 'org.anarres.stdproject'
stdproject {
	javadocLink "https://mina.apache.org/mina-project/apidocs/"
	javadocLink "http://netty.io/4.0/api/"
	javadocLink "https://kaitoy.github.io/pcap4j/javadoc/latest/en/"
	javadocGroup "Service Interfaces and Common code", "org.anarres.dhcp.common*",
			"org.apache.directory.server.dhcp.service*",
			"org.apache.directory.server.dhcp"
	javadocGroup "DHCPv4 Protocol", "org.apache.directory.server.dhcp.options*",
			"org.apache.directory.server.dhcp.io*",
			"org.apache.directory.server.dhcp.messages*"
	javadocGroup "DHCPv6 Protocol", "org.anarres.dhcp.v6*"
	javadocGroup "Netty I/O Adapter", "org.apache.directory.server.dhcp.netty*"
	javadocGroup "Mina I/O Adapter", "org.apache.directory.server.dhcp.mina*"
	javadocGroup "Pcap4J I/O Adapter", "org.anarres.dhcp.server.pcap"
}

subprojects {
	group = "org.anarres.dhcp"

	apply plugin: 'org.anarres.stdmodule'
	stdmodule {
		description "A high-performance pure Java DHCP server."
		author id: 'shevek', name: 'Shevek', email: 'github@anarres.org'
		author id: 'marosmars', name: 'Maros', email: 'maros.mars@gmail.com'
		license 'Apache-2.0'
	}

	dependencies {
		compile 'com.google.code.findbugs:annotations:2.0.3'
		compile 'org.slf4j:slf4j-api:1.7.12'
	}

	sourceCompatibility = 1.7
	animalsniffer {
		signature = "org.codehaus.mojo.signature:java17:+@signature"
	}
}

project(':dhcp-protocol') {
	dependencies {
		compile 'com.google.guava:guava:18.0'
		// compile 'org.codehaus.mojo:animal-sniffer-annotations:1.14'
		// compile 'dnsjava:dnsjava:2.1.1'
		compile "org.anarres.jallocator:jallocator:1.0.1"
	}
}

project(':dhcp-server-mina') {
	dependencies {
		compile project(':dhcp-protocol')
		compile 'org.apache.mina:mina-core:2.0.8'

		testCompile project(':dhcp-protocol').sourceSets.test.output
	}
}

project(':dhcp-server-netty') {
	dependencies {
		compile project(':dhcp-protocol')
		compile 'io.netty:netty-all:4.0.23.Final'

		testCompile project(':dhcp-protocol').sourceSets.test.output
	}
}

project(':dhcp-server-pcap') {
	dependencies {
		compile project(':dhcp-protocol')
		compile 'org.pcap4j:pcap4j-packetfactory-static:1.3.0'

		testCompile project(':dhcp-protocol').sourceSets.test.output
	}
}
